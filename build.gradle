plugins {
	id 'maven-publish'
	id 'com.github.hierynomus.license' version '0.16.1'
	id 'net.fabricmc.fabric-loom-remap' version '1.15-SNAPSHOT'
}

repositories {
	maven {
		url = 'https://maven.parchmentmc.org'
		content { includeGroup('org.parchmentmc.data') }
	}
	maven {
		url = 'https://jitpack.io'
		content { includeGroupAndSubgroups('com.github') }
	}
	maven {
		url = 'https://maven.fallenbreath.me/releases'
		content { includeGroup('me.fallenbreath') }
	}
}

dependencies {
	// loom
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// forge mimic
	compileOnly("org.apache.maven:maven-artifact:3.9.12")
	compileOnly project(":stubs")
}

String MIXIN_CONFIG_PATH = 'turboserverlist.mixins.json'
String LANG_DIR = 'assets/turboserverlist/lang'
JavaVersion JAVA_COMPATIBILITY = JavaVersion.VERSION_1_8
JavaVersion MIXIN_COMPATIBILITY_LEVEL = JAVA_COMPATIBILITY

loom {
	def commonVmArgs = ['-Dmixin.debug.export=true', '-Dmixin.debug.countInjections=true']
	runConfigs.configureEach {
		// to make sure it generates all "Minecraft Client (:subproject_name)" applications
		ideConfigGenerated = true
		runDir '../../run'
		vmArgs commonVmArgs
	}
}

String modVersionSuffix = ''
String artifactVersion = project.mod_version
String artifactVersionSuffix = ''
// detect github action environment variables
// https://docs.github.com/en/actions/learn-github-actions/environment-variables#default-environment-variables
if (System.getenv("BUILD_RELEASE") != "true") {
	String buildNumber = System.getenv("BUILD_ID")
	modVersionSuffix += buildNumber != null ? ('+build.' + buildNumber) : '-SNAPSHOT'
	artifactVersionSuffix = '-SNAPSHOT'  // A non-release artifact is always a SNAPSHOT artifact
}
String fullModVersion = project.mod_version + modVersionSuffix
String fullProjectVersion, fullArtifactVersion

// Example version values:
//   project.mod_version     1.0.3                      (the base mod version)
//   modVersionSuffix        +build.88                  (use github action build number if possible)
//   artifactVersionSuffix   -SNAPSHOT
//   fullModVersion          1.0.3+build.88             (the actual mod version to use in the mod)
//   fullProjectVersion      v1.0.3+build.88            (in build output jar name)
//   fullArtifactVersion     1.0.3-SNAPSHOT             (maven artifact version)

group = project.maven_group
base.archivesName = project.archives_base_name
fullProjectVersion = 'v' + project.mod_version + modVersionSuffix
fullArtifactVersion = artifactVersion + artifactVersionSuffix
version = fullProjectVersion

// See https://youtrack.jetbrains.com/issue/IDEA-296490
// if IDEA complains about "Cannot resolve resource filtering of MatchingCopyAction" and you want to know why
processResources {
	def modProperties = [
			"id"                  : project.mod_id,
			"name"                : project.mod_name,
			"version"             : fullModVersion,
	]
	inputs.properties(modProperties)

	filesMatching([
			"fabric.mod.json",
			"pack.mcmeta",
			"META-INF/mods.toml",
			"META-INF/neoforge.mods.toml",
	]) {
		expand modProperties
	}

	filesMatching(MIXIN_CONFIG_PATH) {
		filter { s -> s.replace('{{COMPATIBILITY_LEVEL}}', "JAVA_${MIXIN_COMPATIBILITY_LEVEL.ordinal() + 1}") }
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
	options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
	if (JAVA_COMPATIBILITY <= JavaVersion.VERSION_1_8) {
		// suppressed "source/target value 8 is obsolete and will be removed in a future release"
		options.compilerArgs << '-Xlint:-options'
	}
}

java {
	sourceCompatibility = JAVA_COMPATIBILITY
	targetCompatibility = JAVA_COMPATIBILITY

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	inputs.property "archives_base_name", project.archives_base_name
	from(rootProject.file('LICENSE')) {
		rename { "${it}_${inputs.properties.archives_base_name}" }
	}

	manifest {
		attributes(MixinConfigs: 'turboserverlist.mixins.json')  // forge needs this
	}
}

// https://github.com/hierynomus/license-gradle-plugin
license {
	// use "gradle licenseFormat" to apply license headers
	header = rootProject.file('HEADER.txt')
	include '**/*.java'
	skipExistingHeaders = true

	headerDefinitions {
		// ref: https://github.com/mathieucarbou/license-maven-plugin/blob/4c42374bb737378f5022a3a36849d5e23ac326ea/license-maven-plugin/src/main/java/com/mycila/maven/plugin/license/header/HeaderType.java#L48
		// modification: add a newline at the end
		SLASHSTAR_STYLE_NEWLINE {
			firstLine = "/*"
			beforeEachLine = " * "
			endLine = " */" + System.lineSeparator()
			afterEachLine = ""
			skipLinePattern = null
			firstLineDetectionPattern = "(\\s|\\t)*/\\*.*\$"
			lastLineDetectionPattern = ".*\\*/(\\s|\\t)*\$"
			allowBlankLines = false
			isMultiline = true
			padLines = false
		}
	}
	mapping {
		java = 'SLASHSTAR_STYLE_NEWLINE'
	}
	ext {
		name = project.mod_name
		author = 'Fallen_Breath'
		year = Calendar.getInstance().get(Calendar.YEAR).toString()
	}
}
classes.dependsOn licenseFormatMain
testClasses.dependsOn licenseFormatTest

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifactId = base.archivesName.get()
			version = fullArtifactVersion
		}
	}

	// select the repositories you want to publish to
	repositories {
	}
}
